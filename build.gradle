plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.8.5'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(8))
	}

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	withJavadocJar()
}

jar {
	from("LICENSE") {
		rename {
			"${it}_${project.archivesBaseName}"
		}
	}
}

bintray {
	// Use findProperty instead of property to ensure missing properties are not errors unless required
	user = findProperty("bintray.user")
	key = findProperty("bintray.key")
	publications = ["mavenJava"]

	pkg {
		repo = "here-be-no-dragons"
		name = "here-be-no-dragons"
		userOrg = "parzivail-modding-team"
		desc = "Disable the \"experimental world settings\" screen"
		websiteUrl = "https://github.com/Parzivail-Modding-Team/HereBeNoDragons"
		issueTrackerUrl = "https://github.com/Parzivail-Modding-Team/HereBeNoDragons/issues"
		vcsUrl = "https://github.com/Parzivail-Modding-Team/HereBeNoDragons.git"
		licenses = ["LGPL-3.0"]
		publicDownloadNumbers = true

		githubRepo = "Parzivail-Modding-Team/HereBeNoDragons"

		version {
			name = project.version
			released = new Date()
			vcsTag = project.version
		}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
			artifact(javadocJar)

			pom {
				name = "Here be no Dragons!"
				description = "Disable the \"experimental world settings\" screen"
				url = "https://github.com/Parzivail-Modding-Team/HereBeNoDragons"
				inceptionYear = "2020"

				licenses {
					license {
						name = "GNU Lesser General Public License 3.0"
						url = "https://www.gnu.org/licenses/lgpl-3.0.en.html"
					}
				}

				organization {
					name = "Parzivail Modding Team"
					url = "https://www.parzivail.com/"
				}

				// TODO
				//developers {
				//
				//}

				scm {
					connection = "scm:git:https://github.com/Parzivail-Modding-Team/HereBeNoDragons.git"
					developerConnection = "scm:git:ssh://git@github.com:Parzivail-Modding-Team/HereBeNoDragons.git"
					url = "https://github.com/Parzivail-Modding-Team/HereBeNoDragons"
					tag = project.version
				}

				issueManagement {
					system = "GitHub"
					url = "https://github.com/Parzivail-Modding-Team/HereBeNoDragons/issues"
				}
			}
		}
	}

	// Select the repositories you want to publish to
	// To publish to maven local, no extra repositories are necessary. Just use the task `publishToMavenLocal`.
	repositories {
		// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	}
}
